apply plugin: 'maven-publish'
apply plugin: 'signing'

def signingProperties = new Properties()
File signingPropertiesFile = project.rootProject.file('publish-signing.properties')
if (signingPropertiesFile.exists()) {
    signingProperties.load(new FileInputStream(signingPropertiesFile))
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                pom {
                    name = project.ext.name
                    artifactId = project.ext.artifactId
                    packaging 'aar'
                    description = project.ext.description
                    url = project.ext.url

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'SAP CX-CDC'
                            name = 'SAP CX-CDC'
                        }
                    }

                    scm {
                        connection = 'scm:git:ssh://git@github.com:SAP/...git'
                        developerConnection = 'scm:git:ssh://git@github.com:SAP/...git'
                        url = 'https://github.com/SAP/..'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "SonatypeSnapshot"
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

                credentials {
                    username signingProperties['ossrhUsername']
                    password signingProperties['ossrhPassword']
                }
            }

            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username signingProperties['ossrhUsername']
                    password signingProperties['ossrhPassword']
                }
            }
        }
    }
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

signing {
    sign publishing.publications
}




